###############################
# Query for generating a search graph for testing
###############################

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX cc: <https://colonialcollections.nl/search#>

CONSTRUCT {
  ?dataset a cc:Dataset ;
    cc:name ?title ;
    cc:publisherIri ?publisher ;
    cc:publisherName ?publisherName ;
    cc:licenseIri ?license ;
    cc:licenseName ?licenseName ;
    cc:description ?description ;
    cc:keyword ?keyword .
}
WHERE {
  ?dataset a dcat:Dataset ;
    dct:title ?title ;
    dct:license ?license ;
    dct:publisher ?publisher .

  OPTIONAL { ?dataset dct:description ?description }
  OPTIONAL { ?dataset dcat:keyword ?keyword }

  ?license a foaf:Document ;
    foaf:name ?licenseName .

  ?publisher a foaf:Organization ;
    foaf:name ?publisherName .
}
