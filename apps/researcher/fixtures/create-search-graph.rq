###############################
# Query for generating a search graph with object descriptions for testing
# Basis of the data model: https://icom.museum/en/resources/standards-guidelines/objectid/
# Publish the query to https://colonial-heritage.triply.cc/data-hub/-/queries/create-search-graph/ for use in production
###############################

PREFIX cc: <https://colonialcollections.nl/search#> # Internal, undocumented ontology
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX schema: <http://schema.org/> # Not the https version!
PREFIX skolem: <https://triplydb.com/.well-known/genid/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?object a cc:Thing ;
    cc:additionalType ?type ;
    cc:about ?subject ;
    cc:name ?name ;
    cc:description ?description ;
    cc:inscription ?inscription ;
    cc:owner ?owner ;
    cc:isPartOf ?dataset ;
    cc:material ?material ;
    cc:technique ?technique ;
    cc:creator ?creator ;
    cc:dateCreatedStart ?beginOfTheBegin ;
    cc:dateCreatedEnd ?endOfTheEnd ;
    # To allow for search actions based on name
    cc:typeName ?typeName ;
    cc:aboutName ?aboutName ;
    cc:ownerName ?ownerName ;
    cc:datasetName ?datasetName ;
    cc:materialName ?materialName ;
    cc:techniqueName ?techniqueName ;
    cc:creatorName ?creatorName .
}
WHERE {
  ?object a crm:E22_Human-Made_Object .

  ####################
  # Type
  ####################

  OPTIONAL {
    ?object crm:P2_has_type ?type .
    ?type a skos:Concept ;
      skos:prefLabel ?typeName ;
      skos:inScheme <http://vocab.getty.edu/aat/> .
    FILTER(LANG(?typeName) = "" || LANGMATCHES(LANG(?typeName), "en")) # TBD: which languages to support?
  }

  ####################
  # Subject
  ####################

  OPTIONAL {
    ?object crm:P62_depicts ?subject .
    ?subject a skos:Concept ;
      skos:prefLabel ?aboutName ;
      skos:inScheme <http://vocab.getty.edu/aat/> .
    FILTER(LANG(?aboutName) = "" || LANGMATCHES(LANG(?aboutName), "en")) # TBD: which languages to support?
  }

  ####################
  # Material
  ####################

  OPTIONAL {
    ?object crm:P45_consists_of ?material .
    ?material a skos:Concept ;
      skos:prefLabel ?materialName ;
      skos:inScheme <http://vocab.getty.edu/aat/> .
    FILTER(LANG(?materialName) = "" || LANGMATCHES(LANG(?materialName), "en")) # TBD: which languages to support?
  }

  ####################
  # Technique
  ####################

  OPTIONAL {
    ?object crm:P32_used_general_technique ?technique .
    ?technique a skos:Concept ;
      skos:prefLabel ?techniqueName ;
      skos:inScheme <http://vocab.getty.edu/aat/> .
    FILTER(LANG(?techniqueName) = "" || LANGMATCHES(LANG(?techniqueName), "en")) # TBD: which languages to support?
  }

  ####################
  # Name
  ####################

  OPTIONAL {
    ?object crm:P1_is_identified_by ?linguisticAppellation .
    ?linguisticAppellation crm:P2_has_type <http://vocab.getty.edu/aat/300404670> ; # Name
      crm:P190_has_symbolic_content ?name .
  }

  ####################
  # Description
  ####################

  OPTIONAL {
    ?object crm:P67i_is_referred_to_by ?linguisticObjectForDescription .
    ?linguisticObjectForDescription crm:P2_has_type <http://vocab.getty.edu/aat/300435416> ; # Description
      crm:P190_has_symbolic_content ?description .
  }

  ####################
  # Inscription
  ####################

  OPTIONAL {
    ?object crm:P128_carries ?linguisticObjectForInscription .
    ?linguisticObjectForInscription crm:P2_has_type <http://vocab.getty.edu/aat/300028702> ; # Inscription
      crm:P190_has_symbolic_content ?inscription .
  }

  ####################
  # Owner
  ####################

  OPTIONAL {
    ?object crm:P52_has_current_owner ?owner .
    ?owner foaf:name ?ownerName .
    FILTER(LANG(?ownerName) = "" || LANGMATCHES(LANG(?ownerName), "en"))
  }

  ####################
  # Part of dataset
  ####################

  OPTIONAL {
    ?object la:member_of ?dataset .
    ?dataset dct:title ?tmpTitle .
    FILTER(LANG(?tmpTitle) = "" || LANGMATCHES(LANG(?tmpTitle), "en"))
    BIND(COALESCE(?tmpTitle, "(No name)"@en) AS ?datasetName) # TBD: add multi-language support?
  }

  ####################
  # Creator
  ####################

  OPTIONAL {
    ?object crm:P108i_was_produced_by/crm:P14_carried_out_by ?creator .
    ?creator schema:name ?creatorName .
  }

  ####################
  # Date created
  ####################

  OPTIONAL {
    ?object crm:P108i_was_produced_by/crm:P4_has_time-span ?timeSpan .
    ?timeSpan crm:P82a_begin_of_the_begin ?beginOfTheBegin ;
      crm:P82b_end_of_the_end ?endOfTheEnd .
  }
}
